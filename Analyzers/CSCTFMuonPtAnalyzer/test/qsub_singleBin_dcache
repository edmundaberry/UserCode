#!/bin/tcsh

## Is this a test?
set test = false

## Setup CMSSW environment variables
set SRC_FOLDER = "/uscms/home/eberry/MuonMomentumStudy/CMSSW_3_2_4/src/"

cd $SRC_FOLDER

eval `scram runtime -csh`

cd ..

## User sets these run variables
set package     = "/uscms/home/eberry/MuonMomentumStudy/CMSSW_3_2_4/src/Analyzers/CSCTFMuonPtAnalyzer/test/"
set exec_name   = singleBin_dcache
set base_dir    = /uscmst1b_scratch/lpc1/3DayLifetime/${user}/
set type        = CSCMuonGunAna
set num_args    = 6

## Let the script set these
set condor_dir  = ${base_dir}/${type}/condor
set log_dir     = ${base_dir}/${type}/log
set output_dir  = ${base_dir}/${type}/output
set exec_path   = ${package}/qrun_${exec_name}

if ($test != true) then

# These variables have to be here
set minJob        = $1
set maxJob        = $2
set ptBinNumBegin = $3
set ptBinNumEnd   = $4
set ptBinNEvents  = $5
set suffix = $6

if ($#argv != $num_args) then
    echo "USAGE: ./${0} <min_job> <max_job> <min_ptBin> <max_ptBin> <num_events> <suffix>"
    exit 1
endif 


else 

# These variables have to be here
set minJob = 2
set maxJob = 2
set ptBinNumBegin = 4
set ptBinNumEnd   = 4
set ptBinNEvents  = 10

## Here is where you would set any other variables you might have
set suffix = TEST

endif

## If input doesn't make sense, print the usage and bail

## Check to make sure all of the executables and directories are where they should be
if (! -e ${condor_dir}) then 
    echo "Warning! Could not find condor directory ${condor_dir}"
    echo "Trying to create it..."
    mkdir -p ${condor_dir}
    if (! -e ${condor_dir}) then 
	echo "Failed to create ${condor_dir}. Exiting."
	exit 1
    endif 
    echo "OK"
endif
if (! -e ${output_dir}) then
    echo "Warning! Could not find condor directory ${output_dir}"
    echo "Trying to create it..."
    mkdir -p ${output_dir}
    if (! -e ${output_dir}) then 
	echo "Failed to create ${output_dir}. Exiting."
	exit 1
    endif 
    echo "OK"
endif 
if (! -e ${log_dir}) then 
    echo "Warning! Could not find condor directory ${log_dir}"
    echo "Trying to create it..."
    mkdir -p ${log_dir}
    if (! -e ${log_dir}) then 
	echo "Failed to create ${log_dir}. Exiting."
	exit 1
    endif 
    echo "OK"
endif 
if (! -e $exec_path) then
    echo "Warning! Cannot find executable: ${exec_path}"
    echo "Exiting."
    exit 1
endif 

cd $output_dir

set job = $minJob

while ($job <= $maxJob)

set ptBin = $ptBinNumBegin

while ($ptBin <= $ptBinNumEnd)
    
    set file_prefix = ${exec_name}_ptBin${ptBin}_job${job}_${ptBinNEvents}events
    set condor_file = ${file_prefix}_condor

    cat > ${condor_dir}/${condor_file}<<EOF
    universe = vanilla
    Executable = ${exec_path}
    Requirements = Memory >= 199 && OpSys == "LINUX" && (Arch != "DUMMY")
    ShouldTransferFiles = YES
    WhenToTransferOutput = ON_EXIT
    Output = ${log_dir}/${file_prefix}_\$(Cluster)_\$(Process).stdout
    Error = ${log_dir}/${file_prefix}_\$(Cluster)_\$(Process).stderr
    Log = ${log_dir}/${file_prefix}_\$(Cluster)_\$(Process).log
    notify_user = ${user}@FNAL.GOV
    Arguments = $job $ptBin $ptBinNEvents $suffix
    Queue 1
  
EOF

    condor_submit ${condor_dir}/${condor_file}

    @ ptBin = $ptBin + 1
    
end
    
    @ job = $job + 1    

end





