#!/bin/tcsh

set work_dir = $PWD
set begin = 0
set end = 0
set useNoise = 0
set usePart = 0

if ($#argv == 4) then
    echo 'Setting working directory to $PWD = ' $PWD
    set begin = $1
    set end = $2
    set usePart = $3
    set useNoise = $4
else if( $#argv == 5 ) then
    if ( -d $1 ) then
	set work_dir = $1
	set begin = $2
	set end = $3
	set usePart = $4
	set useNoise = $5
    else
	echo "Error! $1 is not a directory, exiting!"
	exit 1
    endif

else
    echo "Usage: $0 <work_dir> <begin_job_num> <end_job_num> <use_noise> OR"
    echo "If desired work directory is the current one, omit directory argument."
    echo "The script assumes there is a sub-directory called log/ in the designated work dir."
    exit 1
endif

cd $work_dir

# Check for the executable!
if (! -e qrun_dataOverlay ) then
    echo "Warning! Could not find executable qrun_dataOverlay, script will exit"
    exit 1
endif
if (! -e log ) then
    echo "Warning! Could not find log directory - please create."
    exit 1
endif

if ($usePart == 0) set sourceName = nuGun
if ($usePart == 1) set sourceName = diJet

echo "Notifications will be sent to $user@fnal.gov"

set count = $begin
while ($count <= $end)

echo $count

cat > dataOverlay_${count}_condor <<EOF
universe = vanilla
Executable = ${CMSSW_BASE}/src/Analyzers/HcalDigiAnalyzer/test/qrun_dataOverlay
Requirements = Memory >= 199 &&OpSys == "LINUX" && (Arch != "DUMMY" )
Should_Transfer_Files = NO
Output =  ${HOME}/nobackup/log/dataOverlay_study_${count}_\$(Cluster)_\$(Process).stdout
Error = ${HOME}/nobackup/log/dataOverlaystudy_${count}_\$(Cluster)_\$(Process).stderr
Log = ${HOME}/nobackup/log/dataOverlay_study_${count}_\$(Cluster)_\$(Process).log
notify_user = ${user}@FNAL.GOV
Arguments = $work_dir 
Queue 1
EOF

condor_submit dataOverlay_${count}_condor
@ count = $count + 1

end
